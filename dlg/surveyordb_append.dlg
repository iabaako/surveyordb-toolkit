/* Title	: surveyordb_append.dlg
   Purpose	: Create Dialog Box for using surveyordb_append.ado
   Date		: jul2018
*/

*! version 0.0.0.180703 Ishmail Azindoo Baako (IPA)
* Written in stata version 15.1; requires stata version 13.0
VERSION 13.0
SYNCHRONOUS_ONLY          
POSITION . . 550 400

DIALOG main, label("Surveyor Database Tool Kit - append") tabtitle("Main")
BEGIN
 GROUPBOX descbox			10	10	520	60	,	label("description")
	TEXT desc1				20  35  500 .	,	///
		label("append new data from field monitoring output to surveyor database")
	
 GROUPBOX append			10  90  520 220	,	label("Options for appending field monitoring output")
	TEXT lab1	 			20	+20	120	.	,	label("Load current surveyor database")
	FILE dbfile				20	+20	500	.	,	label("Browse ..") error("Select Database File") filter("Stata Datasets (*.dta)")
	TEXT lab2	 			20	+20	120	.	,	label("Load Contract Request Form")
	FILE conreqfile			20	+20	500	.	,	label("Browse ..") error("Select Contract Request File") filter("Excel Files (*.xlsx, *.xls)")
	TEXT sheetlab			20	+30	220	10	,	label("Specify Contract Request Sheet")
	EDIT sheet				230	@	180	20	,	label(sheename) default(Request)
	TEXT lab3	 			20	+20	220	.	,	label("Select Field Monitoring Output File")
	FILE fmofile			20	+20	500	.	,	label("Browse ..") error("Select Field Monitoring Output File") filter("Excel Files (*.xlsx, *.xls)")
	TEXT lab4	 			20	+20	120	.	,	label("Save new surveyor database as")
	FILE savefile			20	+20	500	.	,	label("Browse ..") error("Select filename for saving new dataset") filter("Stata Datasets (*.dta)") save

 GROUPBOX options			10  330 520 50	,	label("Other Options")
	CHECKBOX optrep			20	+20 .	.	, 	label("Replace if new dataset exist") option(replace) 
END

OK      ok1,    label("OK")
CANCEL  can1,   label("Cancel")
SUBMIT  sub1
COPY    copy1
HELP    hlp1,   view("help surveyordb_update")
RESET   res1,   label("Reset")

PROGRAM command
BEGIN
    put "surveyordb append using "
    require main.dbfile 
    put `"""' main.dbfile `"""' 
	put ", request("
    require main.conreqfile
    put main.conreqfile
	put ") sheet("
	require main.sheet
	put main.sheet
    put ") fmoutput("
	require main.fmofile
	put main.fmofile
	put ") save("
	require main.savefile
	put main.savefile
	put ")"
		option main.optrep
END
